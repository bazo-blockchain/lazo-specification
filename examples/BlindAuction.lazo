version 1.1

contract BlindAuction {

    struct Bid {
        String blindedBid
        int deposit
    }

    Payable beneficiary
    int biddingEnd
    int revealEnd
    bool ended

    Map<address, Bid[]> bids
    Map<address, int> totalBids
    readonly int maxBids = 10

    address highestBidder
    int highestBid

    Map<address, int> pendingReturns

    event AuctionEnded(address winner, int highestBid)

    constructor(int biddingTime, int revealTime, address beneficiary) {
        this.beneficiary = (Payable) beneficiary
        biddingEnd = block.timestamp + biddingTime
        revealEnd = biddingEnd + revealTime
    }

    [ReadOnly]
    internal function bool onlyBefore(int time) {
        return block.timestamp < time
    }

    [ReadOnly]
    internal function bool onlyAfter(int time) {
        return block.timestamp > time
    }

    [Payable]
    [Pre: onlyBefore(biddingEnd)]
    [Pre: totalBids[msg.sender] < maxBids]
    function void bid(String blindedBid) {
        if(!bids.contains(msg.sender)){
            bids[msg.sender] = new Bid[maxBids]
        }
        int count = totalBids[msg.sender]
        bids[msg.sender][count] = new Bid(blindedBid, msg.value)
        totalBids[msg.sender] = count + 1
    }

    [Pre: onlyAfter(biddingEnd)]
    [Pre: onlyBefore(revealEnd)]
    [Pre: values.length == bids[msg.sender].length]
    [Pre: fakes.length == bids[msg.sender].length]
    [Pre: secrets.length == bids[msg.sender].length]
    function void reveal(int[] values, bool[] fakes, String[] secrets) {
        int refund
        foreach (i, Bid bidToCheck : bids[msg.sender]) {
            int _value = values[i]
            bool _fake = fakes[i]
            String _secret = secrets[i]

            if (bidToCheck.blindedBid != sha3((String) _value + (String) _fake + _secret)) {
                continue
            }

            refund += bidToCheck.deposit
            if (!_fake && bidToCheck.deposit >= _value) {
                if (placeBid(msg.sender, _value)){
                    refund -= _value
                }
            }
            bidToCheck.blindedBid = ""
        }

        Payable sender = (Payable) msg.sender
        sender.transfer().send(new MsgArgs(coins=refund))
    }

    internal function bool placeBid(address bidder, int value) {
        if (value <= highestBid) {
            return false
        }

        if (highestBidder != 0x0) {
            pendingReturns[highestBidder] += highestBid
        }

        highestBid = value
        highestBidder = bidder
        return true
    }

    function void withdraw() {
        int amount = pendingReturns[msg.sender]
        if (amount > 0) {
            pendingReturns[msg.sender] = 0

            Payable sender = (Payable) msg.sender
            sender.transfer().send(new MsgArgs(coins=amount))
        }
    }

    [Pre: onlyAfter(revealEnd)]
    [Pre: !ended]
    function void auctionEnd() {
        emit AuctionEnded(highestBidder, highestBid)
        ended = true
        beneficiary.transfer().send(new MsgArgs(coins=highestBid))
    }

}